plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
    id 'io.freefair.lombok' version '8.13.1'
}

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://maven.enginehub.org/repo/" }
    maven {
        url = "https://repo.boatlabs.net/repository/maven/"
        credentials {
            username = "${boatlabs_repo_user}"
            password = "${boatlabs_repo_password}"
        }
    }
    maven { url = "https://jitpack.io" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'me.makkuusen.timing.system:TimingSystem:385'
    compileOnly 'com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.12.3'

    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'co.aikar:taskchain-bukkit:3.7.2'
}

processResources {
    def properties = [
            'version': version
    ]

    inputs.properties properties

    filesMatching('plugin.yml') {
        expand properties
    }
}

build {
    dependsOn tasks.named('shadowJar')
}

java {
    def javaVersion = JavaVersion.toVersion(21)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier = ''

    relocate 'co.aikar', 'me.pigalala.trackexchange.shaded'
}
